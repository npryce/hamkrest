group 'com.natpryce'
version (hasProperty("-version") ? property("-version") : "SNAPSHOT")

println "building version ${version}"

buildscript {
    ext.kotlin_version = '1.0.0-beta-1103'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.1"
    }
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile 'junit:junit:4.12'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'hamkrest',
                'Implementation-Vendor': 'com.natpryce',
                'Implementation-Version': version
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}


dokka {
    outputFormat 'javadoc'
    outputDirectory 'build/javadoc'
}

task javadocJar(type: Jar, dependsOn: 'dokka') {
    classifier 'javadoc'
    from 'build/javadoc'
}

test {
    include 'com/natpryce/hamkrest/**'
    scanForTestClasses true
    reports {
        junitXml.enabled = true
        html.enabled = true
    }

    beforeTest { desc ->
        print "${desc.className} ${desc.name}"
    }
    afterTest { desc, result ->
        println " -> ${result.resultType}"
    }
}

artifacts {
    archives sourcesJar, javadocJar
}

signing {
    required { hasProperty("sign") || gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    if (hasProperty('ossrhUsername') && hasProperty('ossrhPassword')) {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'HamKrest'
                    packaging 'jar'
                    description 'Matcher library for Kotlin. A port of Hamcrest, made more Kotlin-esque.'
                    url 'https://github.com/npryce/hamkrest'

                    scm {
                        connection 'git@github.com:npryce/hamkrest.git'
                        url 'https://github.com/npryce/hamkrest'
                    }

                    licenses {
                        license {
                            name 'Apache 2.0'
                            url 'http://opensource.org/licenses/Apache-2.0'
                        }
                    }

                    developers {
                        developer {
                            id 'npryce'
                            name 'Nat Pryce'
                        }
                        developer {
                            id 'dmcg'
                            name 'Duncan McGregor'
                        }
                    }
                }
            }
        }
    }
    else {
        println("no OSSRH username and/or password!")
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}
